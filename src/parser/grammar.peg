
Start
  = ChildNodes

ChildNodes
  = (Text/Element)*

Element
  = startTag:StartTag children:ChildNodes endTag:EndTag {

    if (startTag.name != endTag.name) {
      expected("</" + startTag.name + ">");
    }

    return {
      type: "element",
      name: startTag.name,
      children: children
    };
  }
  / StartTag
  / StartEndTag



Text
  = chars:[^<]+ { 
      return {
        type: "text",
        value: chars.join("")
      };
    }


StartTag
  = "<" info:TagInfo ">" { return info; }

StartEndTag
  = "<" info:TagInfo "/>" { return info; }

TagInfo
  = name:TagName Zs* attributes:Attribute* {
      return {
        name: name,
        attributes: attributes
      };
    }

EndTag
  = "</" name:TagName ">" {
      return {
        name: name
      };
    }

Attribute
  = name:AttributeName "=" string:StringLiteral {
      return {
        name: name,
        value: string.value
      };
    }
  / name:AttributeName {
      return {
        name: name
      }
    }


AttributeName
  = Zs* name:Word { return name; }

Zs = [\u0020\u00A0\u1680\u2000-\u200A\u202F\u205F\u3000]

StringLiteral "string"
  = '"' chars:DoubleStringCharacter* '"' {
      return { type: "StringLiteral", value: chars.join("") };
    }
  / "'" chars:SingleStringCharacter* "'" {
      return { type: "StringLiteral", value: chars.join("") };
    }


DoubleStringCharacter
  = !('"' / "\\") SourceCharacter { return text(); }
  / '\\"'


SingleStringCharacter
  = !("'" / "\\") SourceCharacter { return text(); }
  / "\\'"

SourceCharacter
  = .

TagName
  = Word

Word
  = chars:[a-z]+ { return chars.join(""); }


